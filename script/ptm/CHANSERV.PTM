addons\chanserv.ppa
; #= ppcsbot -rs
; ########################################
; Peace and Protection
; ChanServ support (standard addon)
; ########################################

#.ppa.info off
[addon]
name=[chanserv:name:dlg]
group=[addon:core:dlg]
popup=[chanserv:name]
author=pai
version=1.02
url=http://www.kristshell.net/pnp/
email=pnp@login.kristshell.net
id=ppcsbot
ppver=4.22
dialogs=csregister
hashcid=-csacc.*
[files]
1=chanserv.ppa
[notes]
1=[chanserv:info1:dlg]
2=[chanserv:info2:dlg]
[menu]
1=$iif($server,$iif($_isserv(cs),[popups_misc:cs_disable:net=$hget(pnp. $+ $cid,net)],[popups_misc:cs_enable:net=$hget(pnp. $+ $cid,net)])):cs $iif($_isserv(cs),off,on)
[interfaces]
bot=_cs.splice
#.ppa.info end

;;; todo- users, akicks, config
;;; auto clear clears one net- no way to clear all nets, clear while offline pointless

; /csa [#chan] [0|1|2|3|no|aop|sop|founder]
; view or set cs access
alias csa {
  if ($server == $null) _error [error:not_connected]
  if ($_isserv(cs) == $false) _error [error:no_chanserv:cmd=/csa]
  if ($_ischan($1)) { var %chan = $1,%how = $2 }
  elseif ($active ischan) { var %chan = $active,%how = $1 }
  else _error [error:in_chan:cmd=/csa]
  if (%how == $null) {
    %how = $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),a $+ %chan)
    if (%how !isnum 0-3) %how = 0
  }
  else {
    if (%how !isnum 0-3) {
      if ($pos(asf,$left(%how,1),1) isnum) %how = $ifmatch
      else %how = 0
    }
    if (%how) writeini $_cfg(pw.ini) cs- $+ $hget(pnp. $+ $cid,net) a $+ %chan %how
    else remini $_cfg(pw.ini) cs- $+ $hget(pnp. $+ $cid,net) a $+ %chan
  }
  var %access = $gettok([chanserv:access_none].[chanserv:access_aop].[chanserv:access_sop].[chanserv:access_founder],$calc(%how + 1),46)
  dispa [chanserv:set_access:access=$;s(%access):chan=$;s(%chan)]
}

; /cs command [#chan|?] [params]
; not mentioned but work- help, drop, acc(ess), acc(ess) nick, info, invite, mkick, mdeop, unban, count
alias cs {
  if (($1 == on) || ($1 == [word:on:lower])) {
    var %net = $2
    if ($2 == $null) %net = $hget(pnp. $+ $cid,net)
    if (%net == [word:offline]) _error [error:cs_connected:cmd=/cs $1]
    set %.net %net
    _ssplay Dialog
    var %opt = $$dialog(csway,csway,-4)
    ; Set on all servers that are this net
    var %scon = $scon(0)
    while (%scon >= 1) {
      scon %scon
      if (($server) && (%net == $hget(pnp. $+ $cid,net))) {
        _addbot cs ChanServ $replace($gettok(%opt,1,32),???,$gettok($server,-2-,46))
      }
      dec %scon
    }
    scon -r
    writeini $_cfg(pw.ini) csforce %net 1 %opt
    dispa [chanserv:enable:net=$;s(%net)]
    return
  }
  if (($1 == off) || ($1 == [word:off:lower])) {
    var %net = $2
    if ($2 == $null) %net = $hget(pnp. $+ $cid,net)
    if (%net == [word:offline]) _error [error:cs_connected:cmd=/cs $1]
    ; Set on all servers that are this net
    var %scon = $scon(0)
    while (%scon >= 1) {
      scon %scon
      if (($server) && (%net == $hget(pnp. $+ $cid,net))) {
        _rembot cs ChanServ
      }
      dec %scon
    }
    scon -r
    remini $_cfg(pw.ini) csforce %net
    dispa [chanserv:disable:net=$;s(%net)]
    return
  }
  if (($_isserv(cs) == $false) && (($1 != auto) || ($istok(view clear,$2,32) == $false))) {
    if ($server) _error [error:no_chanserv:cmd=/cs]
    _error [error:not_connected]
  }
  var %chan,%param,%send,%pw,%old,%nets,%net,%type,%who
  if ($1 == $null) _qhelp $_s2f(/cs +[quick:p_command:s2p] -#[word:channel:lower:s2p] -[quick:p_param:s2p] * ![quick:cs])
  elseif ($_ischan($2)) { %chan = $2 | %param = $3- }
  elseif (($istok(list help,$1,32)) || (($1 == auto) && ($istok(add del,$2,32) == $false))) %param = $2-
  elseif ($active ischan) { %chan = $active | %param = $2- }
  elseif ($2 == ?) { %chan = $_patch($_entry(-1,$null,[chanserv:chan_prompt:cmd=$1])) | %param = $3- }
  else _error [error:in_chan:cmd=/cs]
  if (($1 == set) && ($numtok(%param,32) == 1)) {
    %send = msgcs SET %chan $upper(%param) | goto %param

    :passwd
    _ssplay Question
    %pw = $$input([chanserv:founder_pw:chan=%chan],2)
    if ($$input([chanserv:verify_pw],2) !=== %pw) _error [error:password]
    %send %pw
    return
    :webpasswd
    _ssplay Question
    %pw = $$input([chanserv:founder_webpw:chan=%chan],2)
    if ($$input([chanserv:verify_pw],2) !=== %pw) _error [error:password]
    %send %pw
    return
    :founder | _okcancel 1 [chanserv:founder_transfer:chan=%chan] ( $+ $me $+ ) | %send | return
    :desc | %send $_entry(0,$null,[chanserv:desc:chan=%chan]) | return
    :mlock | %param = $_entry(1,$_chmode(%chan),[chanserv:mode_lock:chan=%chan]) | %send $iif(%param,%param,*) | return
    :opguard | var %yn = $_yesno(1,[chanserv:op_guard]) | %send $iif(%yn,ON,OFF) | return
    :keeptopic | var %yn = $_yesno(1,[chanserv:keep_topic]) | %send $iif(%yn,ON,OFF) | return
    :url | %send $_entry(1,$null,[chanserv:url:chan=%chan]) | return
    :ident | var %yn = $_yesno(1,[chanserv:ns_identify]) | %send $iif(%yn,ON,OFF) | return
    :restrict | var %yn = $_yesno(0,[chanserv:restrict_op:chan=%chan]) | %send $iif(%yn,ON,OFF) | return
    :topiclock | set %.fullmsg [chanserv:change_topic] | set %.found [chanserv:access_founder],[chanserv:change_any_sop],[chanserv:change_any_op] | _ssplay Question | %param = $$dialog(nickcomp,nickcomp,-4) | %send $gettok(FOUNDER SOP OFF,$findtok([chanserv:access_founder].[chanserv:change_any_sop].[chanserv:change_any_op],%param,1,46),32) | return
    :leaveops | var %yn = $_yesno(0,[chanserv:leave_op:chan=%chan]) | %send $iif(%yn,ON,OFF) | return
    :unsecure | var %yn = $_yesno(0,[chanserv:unsecure_founder]) | %send $iif(%yn,ON,OFF) | return
    :private | var %yn = $_yesno(0,[chanserv:private:chan=%chan]) | %send $iif(%yn,ON,OFF) | return
    :verbose | var %yn = $_yesno(0,[chanserv:verbose]) | %send $iif(%yn,ON,OFF) | return
    :successor | %send $_entry(0,$null,[chanserv:successor:chan=%chan]) | return
    :mailblock | var %yn = $_yesno(0,[chanserv:mailblock]) | %send $iif(%yn,ON,OFF) | return
    :memo | set %.fullmsg [chanserv:send_memo] | set %.found [chanserv:access_founder],[chanserv:change_any_sop],[chanserv:change_any_aop],[chanserv:change_none] | _ssplay Question | %param = $$dialog(nickcomp,nickcomp,-4) | %send $gettok(FOUNDER SOP AOP NONE,$findtok([chanserv:access_founder].[chanserv:change_any_sop].[chanserv:change_any_aop].[chanserv:change_none],%param,1,46),32) | return
    :%param
    msgcs $upper($1) %chan
  }
  elseif ($1 == auto) {
    if ($gettok(%param,1,32) == add) {
      if (%chan == $null) _error [error:in_chan:cmd=/cs auto add]
      if ($gettok(%param,2,32)) %pw = $ifmatch
      else {
        _ssplay Question
        %pw = $$input([chanserv:login_pw:chan=%chan] ( $+ $hget(pnp. $+ $cid,net) $+ ),2)
        if ($$input([chanserv:verify_pw],2) !=== %pw) _error [error:password]
      }
      writeini $_cfg(pw.ini) cs- $+ $hget(pnp. $+ $cid,net) %chan $_pw.enc(%pw)
      dispa [chanserv:auto_add:bot=$;t(ChanServ):chan=$;s(%chan):net=$hget(pnp. $+ $cid,net)]
      %old = $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),all)
      writeini $_cfg(pw.ini) cs- $+ $hget(pnp. $+ $cid,net) all $addtok(%old,%chan,32)
      %old = $readini($_cfg(pw.ini),n,cs,nets)
      writeini $_cfg(pw.ini) cs nets $addtok(%old,$hget(pnp. $+ $cid,net),32)
    }
    elseif ($gettok(%param,1,32) == del)  {
      if (%chan == $null) _error [error:in_chan:cmd=/cs auto del]
      %pw = $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),%chan)
      if (%pw) {
        %old = $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),all)
        %old = $remtok(%old,%chan,1,32)
        if (%old == $null) {
          remini $_cfg(pw.ini) cs- $+ $hget(pnp. $+ $cid,net) all
          %old = $readini($_cfg(pw.ini),n,cs,nets)
          %old = $remtok(%old,$hget(pnp. $+ $cid,net),1,32)
          if (%old == $null) remini $_cfg(pw.ini) cs nets
          else writeini $_cfg(pw.ini) cs nets %old
        }
        else writeini $_cfg(pw.ini) cs- $+ $hget(pnp. $+ $cid,net) all %old
        remini $_cfg(pw.ini) cs- $+ $hget(pnp. $+ $cid,net) %chan
        dispa [chanserv:auto_del:chan=$;s(%chan):net=$hget(pnp. $+ $cid,net)]
      }
      else dispa [chanserv:auto_not:chan=$;s(%chan):net=$hget(pnp. $+ $cid,net)]
    }
    elseif ($gettok(%param,1,32) == view) {
      %nets = $readini($_cfg(pw.ini),n,cs,nets)
      if (%nets == $null) dispa [chanserv:auto_none:bot=ChanServ]
      else {
        _info /cs auto
        dispr @Info [chanserv:login_head:bot=ChanServ]-
        :vloop1
        %net = $gettok(%nets,1,32)
        %param = $readini($_cfg(pw.ini),n,cs- $+ %net,all)
        :vloop2
        %chan = $gettok(%param,1,32)
        dispr @Info   - [chanserv:login_list:bot=$;t(ChanServ):chan=$;s(%chan):net=%net]
        %param = $gettok(%param,2-,32)
        if (%param) goto vloop2
        %nets = $gettok(%nets,2-,32)
        if (%nets) goto vloop1
      }
    }
    elseif ($gettok(%param,1,32) == clear) {
      remini $_cfg(pw.ini) cs- $+ $hget(pnp. $+ $cid,net)
      %old = $readini($_cfg(pw.ini),n,cs,nets)
      %old = $remtok(%old,$hget(pnp. $+ $cid,net),1,32)
      if (%old == $null) remini $_cfg(pw.ini) cs nets
      else writeini $_cfg(pw.ini) cs nets %old
      dispa [chanserv:auto_purge:bot=ChanServ:net=$hget(pnp. $+ $cid,net)]
    }
    else {
      if (%chan) {
        %pw = $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),%chan)
        if (%pw) { .msgcs IDENTIFY %chan $_pw.enc(%pw) | disprc %chan [chanserv:login_do:bot=$;t(ChanServ)] }
        else _error [error:cs_nologin:bot=ChanServ:chan=%chan]
      }
      else {
        %param = $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),all)
        if (%param == $null) _error [error:cs_nologin_any:bot=ChanServ]
        :aloop
        %chan = $gettok(%param,1,32)
        %pw = $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),%chan)
        .msgcs IDENTIFY %chan $_pw.enc(%pw)
        disprc %chan [chanserv:login_do:bot=$;t(ChanServ)]
        %param = $gettok(%param,2-,32)
        if (%param) goto aloop
      }
    }
  }
  ;!! Not done yet
  elseif ($1 == users) { _error not done yet... }
  elseif ($1 == akicks) { _error not done yet... }
  elseif ($1 == config) { _error not done yet... }
  elseif (($numtok(%param,32) <= 1) && ($istok(sop aop akick op deop sendpass,$1,32))) {
    goto $1

    :sop | :aop | %type = $upper($1) | goto ask
    :akick | %type = AutoKick | goto ask
    :ask
    if ($findtok(Add Del List Wipe Clean,%param,1,32)) {
      %send = msgcs $upper($1) %chan %param
      goto $gettok(%param,1,32)
    }
    editbox -ap /cs $1 %chan
    dispa [chanserv:help_actions]:
    if ($1 == akick) {
      dispa - $:l(Add Del List Wipe)
      ;!!dispa [chanserv:help_interactive:cmd=/cs akicks]
    }
    else {
      dispa - $:l(Add Del List Wipe)
      ;!!dispa [chanserv:help_interactive:cmd=/cs users]
    }
    return
    :add | %send $_entry(-1,$null,[chanserv:add:type=%type]) | return
    :del | %send $_entry(-1,$null,[chanserv:delete:type=%type]) | return
    :list | %send $_entry(1,$null,[chanserv:list:type=%type]) | return
    :wipe | _okcancel 1 [chanserv:purge:type=%type:chan=%chan] | %send | return
    :clean | %send | return

    :op | :deop
    if (%param) %who = $_nccs(32,%chan,$_c2s(%param))
    else %who = $_c2s($_entry(0,$null,$iif($1 == op,[chanserv:op],[chanserv:deop])))
    msgcs $upper($1) %chan %who
    return

    :sendpass
    %send $_entry(-1,$email,[nickserv:forgot_email])
    return
  }
  elseif ($istok(list help,$1,32)) msgcs $upper($1) %param
  elseif (%param) msgcs $upper($1) %chan %param
  elseif ($istok(register identify unban why set drop,$1,32)) {
    %send = msgcs $upper($1) %chan | goto $1

    :drop | _okcancel 0 [chanserv:drop:chan=%chan] | %send | return
    :register | %.csr.chan = %chan | _ssplay Dialog | return $dialog(csregister,csregister,-4)
    :identify | _ssplay Question | %send $$input([chanserv:founder_login],2) | return
    :why | %send $_entry(-1,$null,[chanserv:why_opped]) | return
    :unban | %send $_entry(-1,$null,[chanserv:unban_prompt]) | return

    :set
    editbox -ap /cs $1 %chan
    dispa [chanserv:help_settings]:
    dispa - $:l(Founder Successor Passwd WebPasswd MLock Private Memo MailBlock)
    dispa - $:l(LeaveOps Unsecure Restrict OpGuard Ident Verbose)
    dispa - $:l(Desc URL KeepTopic TopicLock)
    ;!!dispa [chanserv:help_interactive:cmd=/cs config]
  }
  else msgcs $upper($1) %chan
}

; /msgcs full command
alias msgcs {
  _linedance . $+ $_botacc(ChanServ) $1-
  if (($1 == register) || (($1 == set) && ($3 == passwd)) || ($1 == identify)) {
    if ($1 == set) {
      dispr $iif($2 ischan,$2,-ai2) [chanserv:do_cmd:bot=$;t(ChanServ)]: $:h($1-3,?????)
      var %pw = $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),$2)
      if (%pw) writeini $_cfg(pw.ini) cs- $+ $hget(pnp. $+ $cid,net) $2 $_pw.enc($4)
    }
    else {
      dispr $iif($2 ischan,$2,-ai2) [chanserv:do_cmd:bot=$;t(ChanServ)]: $:h($puttok($1-,?????,3,32))
      if (($1 == identify) || ($1 == register)) hadd pnp. $+ $cid -csacc. $+ $2 fop
    }
  }
  else {
    if ($1 == drop) hdel pnp. $+ $cid -csacc. $+ $2
    dispr $iif($2 ischan,$2,-ai2) [chanserv:do_cmd:bot=$;t(ChanServ)]: $:h($1-)
  }
}

alias _cs.splice {
  if ($1 == ?) {
    if ($_isserv(cs) == $false) return 0
    if ($istok(voice devoice halfop dehalfop mode kick topic,$3,32)) return 0
    if ($hget(pnp. $+ $cid,-csacc. $+ $2)) return 1
    var %access = $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),a $+ $2)
    if ((%access > 1) || ((%access == 1) && ($3 != ban))) return 1
    return 0
  }
  goto $3
  :op | :opme | cs op $2 $4- | return
  :deop | cs deop $2 $4- | return
  :ban | cs akick $2 add $4
  :unban | cs unban $2 $4- | return
}

dialog csregister {
  title "[chanserv_dialog:title]"
  option dbu
  size -1 -1 170 137
  box "", 1, 2 4 165 30
  edit "", 3, 7 15 155 11, autohs
  box "[word:password:dlg]:", 21, 2 37 165 52
  text "[chanserv_dialog:pw_instruction]", 20, 10 47 150 15
  text "&[word:password:dlg]:", 8, 5 65 39 8, right
  edit "", 9, 45 63 61 11, pass autohs
  text "&[dialog:confirm]:", 10, 5 76 39 8, right
  edit "", 11, 45 75 61 11, pass autohs
  box "[chanserv_dialog:auto_head]:", 12, 2 93 165 22
  check "&[chanserv_dialog:auto_option]", 13, 15 103 150 8
  button "[dialog:ok]", 101, 15 121 60 12, OK default
  button "[dialog:cancel]", 102, 92 121 60 12, cancel
  edit %.csr.chan, 201, 1 1 1 1, hide autohs
}
on *:DIALOG:csregister:init:*:{
  did -c $dname 13
  did -a $dname 1 [chanserv_dialog:desc:chan=$did(201):notdlg] $+ :
  unset %.csr.chan
}
on *:DIALOG:csregister:sclick:101:{
  if ($did(3) == $null) _error [error:cs_desc]
  if ($did(9) == $null) _error [error:cs_pw]
  if ($did(9) !=== $did(11)) _error [error:password]
  msgcs REGISTER $did(201) $did(9) $did(3)
  if ($did(13).state) cs auto $did(201) add $did(9)
}

on *:SIGNAL:PNP.SIGNON:{ _cs.connect }
on *:LOAD:{
  ; PnP check
  if (!$_ispnp) {
    echo 4 -ati2 *** This addon requires Peace and Protection by pai to use.
    echo 4 -ati2 *** You can download Peace and Protection at http://www.kristshell.net/pnp/
    .timer -mio 1 0 .unload -rs " $+ $script $+ "
    halt
  }
  ; Invalid load method check
  if (!$istok($_cfgx(addons,ids),$readini($script,n,addon,id),32)) {
    .timer -mio 1 0 .unload -rs " $+ $script $+ "
    dispa [startup:addon_file_improper:file=$script]
    halt
  }
  scon -at1 _cs.connect
}

alias _cs.connect {
  if ($readini($_cfg(pw.ini),n,csforce,$hget(pnp. $+ $cid,net))) {
    var %opt = $ifmatch
    _addbot cs ChanServ $replace($gettok(%opt,2,32),???,$gettok($server,-2-,46))
  }

  if ($_isserv(cs)) .cs auto
}

; if bot joins, relogin to chanserv. (most networks don't actually have ChanServ join, though.)
on *:JOIN:#:if (($_isserv(cs)) && ($_isbot($nick)) && ($nick == ChanServ)) .cs auto $chan

alias -l cspop {
  if ($_isserv(cs) == $false) unset %.cspop
  else {
    set -u1 %.cspop [popups_chan:chanserv]
    set -u1 %.csfounder $iif($readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),$active),1,0)
    set -u1 %.csaccess $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),a $+ $active)
    if (%.csaccess isnum 1-2) unset %.csfounder
    if (%.csaccess == $null) set -u1 %.csaccess 0
    return $_popssep
  }
}
menu channel {
  $cspop
  %.cspop
  .$iif(%.csaccess < 1,[popups_chan:cs_info]):cs info
  .-
  .$iif(%.csfounder,[popups_chan:cs_founder])
  ..[popups_chan:cs_login]...:cs identify
  ..[popups_chan:cs_pw]...:cs set passwd
  ..-
  ..[popups_chan:cs_transfer]...:cs set founder
  ..[popups_chan:cs_successor]...:cs set successor
  ..[popups_chan:cs_recover_pass]...:cs sendpass
  ..[popups_chan:cs_drop]...:cs drop
  ..-
  ..[popups_chan:cs_auto_perform]:cs auto #
  ..[popups_chan:cs_auto_del:chan=$replace($active,&,&&)]...:_okcancel 1 [chanserv:confirm_del] | cs auto del
  ..-
  ..[popups_chan:cs_auto_view]:cs auto view
  ..[popups_chan:cs_auto_purge]...:_okcancel 1 [chanserv:confirm_purge:bot=ChanServ] | cs auto clear
  .$iif(%.csfounder == 0,[popups_chan:cs_founder])
  ..[popups_chan:cs_register]...:cs register
  ..[popups_chan:cs_drop]...:cs drop
  ..-
  ..[popups_chan:cs_identify]...:cs identify
  ..[popups_chan:cs_pw]...:cs set passwd
  ..-
  ..[popups_chan:cs_auto_add:chan=$replace($active,&,&&)]...:cs auto add
  ..-
  ..[popups_chan:cs_auto_view]...:cs auto view
  ..[popups_chan:cs_auto_purge]...:_okcancel 1 [chanserv:confirm_purge:bot=ChanServ] | cs auto clear
  .$iif(%.csaccess,[popups_chan:cs_info])
  ..[popups_chan:cs_youraccess]:cs access
  ..[popups_chan:cs_chaninfo]:cs info
  ..[popups_chan:cs_count]:cs count
  .-
  .$iif(%.csaccess,[popups_chan:cs_aop])
  ..$iif(%.csaccess > 1,[popups_chan:cs_add]..):cs aop add
  ..$iif(%.csaccess > 1,[popups_chan:cs_del]...):cs aop del
  ..[popups_chan:cs_list]..:cs aop list
  ..-
  ..$iif(%.csaccess > 2,[popups_chan:cs_purge]...):cs aop wipe
  .$iif(%.csaccess,[popups_chan:cs_sop])
  ..$iif(%.csaccess > 2,[popups_chan:cs_add]...):cs sop add
  ..$iif(%.csaccess > 2,[popups_chan:cs_del]...):cs sop del
  ..[popups_chan:cs_list]...:cs sop list
  ..-
  ..$iif(%.csaccess > 2,[popups_chan:cs_purge]...):cs sop wipe
  ;!!.$iif(%.csaccess > 1,[popups_chan:cs_edit]):cs users
  ;!!.$iif(%.csaccess == 1,[popups_chan:cs_list]):cs users
  .-
  .$iif(%.csaccess,[popups_chan:cs_akick])
  ;!!..$iif(%.csaccess > 1,[popups_chan:cs_list]):cs akicks
  ..-
  ..$iif(%.csaccess > 1,[popups_chan:cs_add]..):cs akick add
  ..$iif(%.csaccess > 1,[popups_chan:cs_del]...):cs akick del
  ..[popups_chan:cs_list]..:cs akick list
  ..-
  ..$iif(%.csaccess > 2,[popups_chan:cs_purge]...):cs akick wipe
  .$iif(%.csaccess,[popups_chan:cs_mass])
  ..[popups_chan:cs_mass_deop]:cs mdeop
  ..[popups_chan:cs_mass_kick]:cs mkick
  .-
  .$iif(%.csaccess > 2,[popups_chan:cs_set])
  ..[popups_chan:cs_desc]...:cs set desc
  ..[popups_chan:cs_url]...:cs set url
  ..[popups_chan:cs_keeptopic]...:cs set keeptopic
  ..[popups_chan:cs_topiclock]...:cs set topiclock
  ..-
  ..[popups_chan:cs_memo]...:cs set memo
  ..[popups_chan:cs_modelock]...:cs set mlock
  ..[popups_chan:cs_private]...:cs set private
  ..-
  ..[popups_chan:cs_leaveop]...:cs set leaveops
  ..[popups_chan:cs_unsecure]...:cs set unsecure
  ..[popups_chan:cs_restrict]...:cs set restrict
  ..[popups_chan:cs_opguard]...:cs set opguard
  ..[popups_chan:cs_ident]...:cs set ident
  ..-
  ..[popups_chan:cs_mailblock]...:cs set mailblock
  ..[popups_chan:cs_verbose]...:cs set verbose
  ..[popups_chan:cs_webpasswd]...:cs set webpasswd
  ;!!.$iif(%.csaccess > 2,[popups_chan:cs_config]...):cs config
  .-
  .[popups_chan:cs_access]
  ..$iif(!%.csaccess,$style(1)) [popups_chan:cs_access_none:chan=$replace($active,&,&&)]:csa # 0
  ..-
  ..$iif(%.csaccess == 1,$style(1)) [popups_chan:cs_access_aop]:csa # 1
  ..$iif(%.csaccess == 2,$style(1)) [popups_chan:cs_access_sop]:csa # 2
  ..$iif(%.csaccess == 3,$style(1)) [popups_chan:cs_access_founder]:csa # 3
}

alias -l cspop2 {
  if ($_isserv(cs) == $false) unset %.cspop
  else {
    set -u1 %.csaccess $readini($_cfg(pw.ini),n,cs- $+ $hget(pnp. $+ $cid,net),a $+ $active)
    if (%.csaccess) set -u1 %.cspop [popups_nick:chanserv]
    return $_popssep
  }
}
menu nicklist {
  $cspop2
  %.cspop
  .[popups_nick:op]:cs op $$snicks
  .[popups_nick:deop]:cs deop $$snicks
  .-
  .$iif(%.csaccess > 1,[popups_nick:cs_akick])
  ..$snick(#,1):cs akick add $$1
  ..-
  ..%.ban2:cs akick add $_ppmask($$1,0,$_getchopt(#,3),2)
  ..%.ban3:cs akick add $_ppmask($$1,0,$_getchopt(#,3),4)
  ..%.ban4:cs akick add $_ppmask($$1,0,$_getchopt(#,3),3)
  ..%.ban5:cs akick add $_ppmask($$1,0,$_getchopt(#,3),6)
  ..-
  ..%.banS:cs akick add $_ppmask($$1,$_stmask(2,#))
  ..%.banD:cs akick add $_ppmask($$1,$_stmask(4,#))
  .-
  .$iif(%.csaccess > 1,[popups_nick:cs_aop])
  ..$snick(#,1):cs aop add $$1
  ..-
  ..%.ban2:cs aop add $_ppmask($$1,0,$_getchopt(#,3),2)
  ..%.ban3:cs aop add $_ppmask($$1,0,$_getchopt(#,3),4)
  ..%.ban4:cs aop add $_ppmask($$1,0,$_getchopt(#,3),3)
  ..%.ban5:cs aop add $_ppmask($$1,0,$_getchopt(#,3),6)
  ..-
  ..%.banS:cs aop add $_ppmask($$1,$_stmask(2,#))
  ..%.banD:cs aop add $_ppmask($$1,$_stmask(4,#))
  .$iif(%.csaccess > 2,[popups_nick:cs_sop])
  ..$snick(#,1):cs sop add $$1
  ..-
  ..%.ban2:cs sop add $_ppmask($$1,0,$_getchopt(#,3),2)
  ..%.ban3:cs sop add $_ppmask($$1,0,$_getchopt(#,3),4)
  ..%.ban4:cs sop add $_ppmask($$1,0,$_getchopt(#,3),3)
  ..%.ban5:cs sop add $_ppmask($$1,0,$_getchopt(#,3),6)
  ..-
  ..%.banS:cs sop add $_ppmask($$1,$_stmask(2,#))
  ..%.banD:cs sop add $_ppmask($$1,$_stmask(4,#))
  .-
  .$iif(%.csaccess > 1,[popups_nick:cs_aop_del])
  ..$snick(#,1):cs aop del $$1
  ..-
  ..%.ban2:cs aop del $_ppmask($$1,0,$_getchopt(#,3),2)
  ..%.ban3:cs aop del $_ppmask($$1,0,$_getchopt(#,3),4)
  ..%.ban4:cs aop del $_ppmask($$1,0,$_getchopt(#,3),3)
  ..%.ban5:cs aop del $_ppmask($$1,0,$_getchopt(#,3),6)
  ..-
  ..%.banS:cs aop del $_ppmask($$1,$_stmask(2,#))
  ..%.banD:cs aop del $_ppmask($$1,$_stmask(4,#))
  .$iif(%.csaccess > 2,[popups_nick:cs_sop_del])
  ..$snick(#,1):cs sop del $$1
  ..-
  ..%.ban2:cs sop del $_ppmask($$1,0,$_getchopt(#,3),2)
  ..%.ban3:cs sop del $_ppmask($$1,0,$_getchopt(#,3),4)
  ..%.ban4:cs sop del $_ppmask($$1,0,$_getchopt(#,3),3)
  ..%.ban5:cs sop del $_ppmask($$1,0,$_getchopt(#,3),6)
  ..-
  ..%.banS:cs sop del $_ppmask($$1,$_stmask(2,#))
  ..%.banD:cs sop del $_ppmask($$1,$_stmask(4,#))
  .-
  .[popups_nick:cs_access]:cs access $$1
  .[popups_nick:cs_why]:cs why $$1
}

;;; menu status { }

;!! add 'advanced' button to specify an actual cmd or address
dialog csway {
  title "[csaccess_dialog:title]"
  option dbu
  size -1 -1 125 77

  text "", 1, 5 5 90 20
  radio "&[csaccess_dialog:option_msg]", 2, 10 22 115 11
  radio "&[csaccess_dialog:option_cmd]", 3, 10 34 115 11
  radio "&[csaccess_dialog:option_secure]", 4, 10 45 115 11

  edit "", 50, 1 1 1 1, autohs hide result

  button "[dialog:ok]", 100, 5 61 40 12, ok default
  button "[dialog:cancel]", 101, 80 61 40 12, cancel
}
on *:DIALOG:csway:init:*:{
  did -c $dname 2
  did -o $dname 1 1 [csaccess_dialog:prompt:net=%.net:notdlg]
  unset %.net
}
on *:DIALOG:csway:sclick:100:did -o $dname 50 1 $iif($did(3).state,ChanServ@/chanserv,$iif($did(4).state,ChanServ@services.???,ChanServ))
